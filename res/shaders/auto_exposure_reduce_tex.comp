#version 430 core

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(std430, binding = 0) buffer OutBuf
{
    vec4 outData[];
};

uniform sampler2D u_Src;
uniform int u_Width;
uniform int u_Height;

shared vec4 sdata[256];

void main()
{
    uvec2 gid = gl_GlobalInvocationID.xy;
    uint lid = gl_LocalInvocationIndex;

    vec4 v = vec4(0.0);
    if (int(gid.x) < u_Width && int(gid.y) < u_Height)
    {
        vec3 c = texelFetch(u_Src, ivec2(gid), 0).rgb;
        v = vec4(c, 1.0);
    }

    sdata[lid] = v;
    barrier();

    for (uint s = 128u; s > 0u; s >>= 1u)
    {
        if (lid < s)
            sdata[lid] += sdata[lid + s];
        barrier();
    }

    if (lid == 0u)
    {
        uint gx = uint((u_Width + 15) / 16);
        uint idx = gl_WorkGroupID.x + gl_WorkGroupID.y * gx;
        outData[idx] = sdata[0];
    }
}

