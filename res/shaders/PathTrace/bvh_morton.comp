#version 460 core
layout(local_size_x=256, local_size_y=1, local_size_z=1) in;

layout(std430, binding=13) buffer Morton { uint code[]; };
layout(std430, binding=14) buffer Index  { uint idx[]; };

uniform uint u_Count;
uniform uint u_K;
uniform uint u_J;

void main()
{
    uint i = gl_GlobalInvocationID.x;
    if (i >= u_Count) return;

    uint ixj = i ^ u_J;
    if (ixj > i && ixj < u_Count)
    {
        bool up = ((i & u_K) == 0u);

        uint a = code[i];
        uint b = code[ixj];

        bool swap = up ? (a > b) : (a < b);
        if (swap)
        {
            code[i] = b;
            code[ixj] = a;

            uint ia = idx[i];
            uint ib = idx[ixj];
            idx[i] = ib;
            idx[ixj] = ia;
        }
    }
}
